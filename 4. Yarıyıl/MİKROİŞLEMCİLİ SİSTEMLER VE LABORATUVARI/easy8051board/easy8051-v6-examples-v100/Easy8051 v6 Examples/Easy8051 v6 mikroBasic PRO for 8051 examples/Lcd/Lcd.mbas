' *
' * Project name:
'     Lcd_Test (Demonstration of the LCD library routines)
' * Copyright:
'     (c) Mikroelektronika, 2009.
' * Revision History:
'     20071210:
'       - initial release;
' * Description:
'     This code demonstrates how to use LCD 4-bit library. LCD is first
'     initialized, then some text is written, then the text is moved.
' * Test configuration:
'     MCU:             AT89S8253
'                      http://www.atmel.com/dyn/resources/prod_documents/doc3286.pdf
'     Dev.Board:       Easy8051 v6
'                      http://www.mikroe.com/en/tools/easy8051-v6/
'     Oscillator:      External Clock 10.0000 MHz
'     Ext. Modules:    Lcd 2x16
'                      http://www.mikroe.com/en/tools/components/#other
'     SW:              mikroBasic PRO for 8051
'                      http://www.mikroe.com/en/compilers/mikrobasic/8051/
' * NOTES:
'     - Turn on LCD backlight switch on development board SW7.6.
'

program  Lcd_Test

' LCD module connections
dim LCD_RS as sbit at P2_0_bit
dim LCD_EN as sbit at P2_1_bit

dim LCD_D4 as sbit at P2_2_bit
dim LCD_D5 as sbit at P2_3_bit
dim LCD_D6 as sbit at P2_4_bit
dim LCD_D7 as sbit at P2_5_bit
' End LCD module connections

dim txt1 as char[16]
    txt2 as char[12]
    txt3 as char[8]
    txt4 as char[7]
    i    as byte                    ' Loop variable

sub procedure Move_Delay()          ' Function used for text moving
  Delay_ms(500)                     ' You can change the moving speed here
end sub

main:
  txt1 = "mikroElektronika"
  txt2 = "Easy8051 v6"
  txt3 = "Lcd4bit"
  txt4 = "example"
  Lcd_Init()                        ' Initialize LCD
  Lcd_Cmd(_LCD_CLEAR)               ' Clear display
  Lcd_Cmd(_LCD_CURSOR_OFF)          ' Cursor off
  LCD_Out(1,6,txt3)                 ' Write text in first row
  LCD_Out(2,6,txt4)                 ' Write text in second row
  Delay_ms(2000)
  Lcd_Cmd(_LCD_CLEAR)               ' Clear display

  LCD_Out(1,1,txt1)                 ' Write text in first row
  LCD_Out(2,4,txt2)                 ' Write text in second row
  Delay_ms(500)

  ' Moving text
  for i = 0 to 3                    ' Move text to the right 4 times
    Lcd_Cmd(_LCD_SHIFT_RIGHT)
    Move_Delay()
  next i

  while TRUE                        ' Endless loop
    for i = 0 to 6                  ' Move text to the left 7 times
      Lcd_Cmd(_LCD_SHIFT_LEFT)
      Move_Delay()
    next i

    for i = 0 to 6                  ' Move text to the right 7 times
      Lcd_Cmd(_LCD_SHIFT_RIGHT)
      Move_Delay()
    next i

  wend
end.
